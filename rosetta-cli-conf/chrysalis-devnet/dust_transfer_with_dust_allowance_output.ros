request_funds(1){
  find_account{
    currency = {"symbol":"IOTA", "decimals":0};
    random_account = find_balance({
      "minimum_balance":{
        "value": "0",
        "currency": {{currency}}
      },
      "create_limit":1
    });
  },

  // Create a separate scenario to request funds so that
  // the address we are using to request funds does not
  // get rolled back if funds do not yet exist.
  request{
    loaded_account = find_balance({
      "account_identifier": {{random_account.account_identifier}},
      "minimum_balance":{
        "value": "1000000",
        "currency": {{currency}}
      },
      "require_coin":true
    });
  }
}

create_account(1){
  create{
    network = {"network":"chrysalis-devnet", "blockchain":"iota"};
    key = generate_key({"curve_type": "edwards25519"});
    account = derive({
      "network_identifier": {{network}},
      "public_key": {{key.public_key}}
    });

    // If the account is not saved, the key will be lost!
    save_account({
      "account_identifier": {{account.account_identifier}},
      "keypair": {{key}}
    });
  }
}

transfer(1){
  transfer{

    // This example shows how to send non-dust (>= 1_000_000 IOTA) from an account A to an account B.
    // Furthermore it shows how to send dust (< 1_000_000 IOTA) to an account C.
    // For an address to receive dust, the following must apply:
    // An address can receive dust only if it has "sufficient space" for dust to receive.
    // Space for dust can only be created by creating outputs of the type `SIG_LOCKED_DUST_ALLOWANCE_OUTPUT` for the address (that should receive dust).
    // `SIG_LOCKED_DUST_ALLOWANCE_OUTPUT` can be created by the owner of the address or by the sender of dust.
    // In this example, the sender is creating the `SIG_LOCKED_DUST_ALLOWANCE_OUTPUT` for the receiver address.
    // Note: an address can only own a certain number of `SIG_LOCKED_DUST_ALLOWANCE_OUTPUT`s.
    // To learn more about the dust protection mechanism, see https://github.com/GalRogozinski/protocol-rfcs/blob/dust/text/0032-dust-protection/0032-dust-protection.md

    transfer.network = {"network":"chrysalis-devnet", "blockchain":"iota"};
    currency = {"symbol":"IOTA", "decimals":0};

    // find sender
    sender = find_balance({
      "minimum_balance":{
        "value": "10000000",
        "currency": {{currency}}
      },
      "require_coin":true
    });

    available_balance = {{sender.balance.value}};
    sender_amount = math({
      "operation": "subtraction",
      "left_value": "0",
      "right_value": {{available_balance}}
    });

    // find dust receiver
    dust_receiver = find_balance({
      "not_account_identifier":[{{sender.account_identifier}}],
      "minimum_balance":{
        "value": "0",
        "currency": {{currency}}
      },
      "create_limit": 100,
      "create_probability": 50
    });

    dust_threshold = "1000000";
    dust_receiver_amount = random_number({
      "minimum": "0",
      "maximum": {{dust_threshold}}
    });
    dust_allowance = "1000000";

    available_balance = {{available_balance}} - {{dust_receiver_amount}};
    available_balance = {{available_balance}} - {{dust_allowance}};

    // find receiver
    receiver = find_balance({
      "not_account_identifier":[{{sender.account_identifier}}, {{dust_receiver.account_identifier}}],
      "minimum_balance":{
        "value": "0",
        "currency": {{currency}}
      },
      "create_limit": 100,
      "create_probability": 50
    });

    receiver_amount = {{available_balance}};

    print_message({"sender":{{sender}}});
    print_message({"receiver":{{receiver}}});
    print_message({"dust receiver":{{dust_receiver}}});

    transfer.confirmation_depth = "1";
    transfer.operations = [
      {
        "operation_identifier":{
          "index":0,
          "network_index":0
        },
        "type":"INPUT",
        "account":{{sender.account_identifier}},
        "amount":{
          "value":{{sender_amount}},
          "currency":{{currency}}
        },
        "coin_change": {
          "coin_identifier": {{sender.coin}},
          "coin_action": "coin_spent"
        }
      },
      {
        "operation_identifier":{
          "index":1
        },
        "type":"SIG_LOCKED_SINGLE_OUTPUT",
        "account":{{receiver.account_identifier}},
        "amount":{
          "value":{{receiver_amount}},
          "currency":{{currency}}
        }
      },
      {
        "operation_identifier":{
          "index":2
        },
        "type":"SIG_LOCKED_SINGLE_OUTPUT",
        "account":{{dust_receiver.account_identifier}},
        "amount":{
          "value":{{dust_receiver_amount}},
          "currency":{{currency}}
        }
      },
      {
        "operation_identifier":{
          "index":3
        },
        "type":"SIG_LOCKED_DUST_ALLOWANCE_OUTPUT",
        "account":{{dust_receiver.account_identifier}},
        "amount":{
          "value":{{dust_allowance}},
          "currency":{{currency}}
        }
      }
    ];
  }
}